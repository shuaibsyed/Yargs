#!/bin/tcsh -f

# CMSC 216, Spring 2023, Project #9
# Public test 8 (public08)
#
# Runs yargs.x with the "mv" utility as its target program, with its
# standard input coming from a file that is contained in the project tarfile
# that has pairs of filenames.  The first file is created by this test, then
# yargs.x runs "mv" to rename it for each line of its standard input.  (Note
# that this test requires yargs.x to ensure that each invocation of the
# target program finishes before the next run, because each invocation uses
# the results of the previous one.)
#
# If you want to debug your code for this test, run gdb yargs.x.  Then
# (perhaps after setting some breakpoints), in gdb, use the command:
#
#   run mv < public08.inputdata
#
# (c) Larry Herman, 2023.  You are allowed to use this code yourself, but
# not to provide it to anyone else.

# Remove these files if they already exist (because this test could be run
# more than once, and the program might have a bug and not remove them).
# Note that "-e" tests whether a file exists in tcsh.
if ((-e public08.file1) || (-e public08.file2) || (-e public08.file3) || \
    (-e public08.file4)) then
  rm -f public08.file?
endif

# now create a file with the first name above (its contents don't matter at
# all)
touch public08.file1

# now use yargs.x to rename the file several times, until its final name is
# the last name above
./yargs.x mv < public08.inputdata

if ($status != 0) then
  # error exit
  echo "Problem!  yargs.x did not exit with status 0."
  exit 9
endif

if (!(-e public08.file4) || (-e public08.file1) || (-e public08.file2) || \
     (-e public08.file3)) then
  # error exit
  echo "Problem!  Something didn't work right (either public08.file4 doesn't"
  echo "exist, or one of public08.file1, public08.file2, or public08.file3"
  echo "do exist)."
  exit 9
else
  # normal exit
  echo "Success!  Only public08.file4 exists, not any of public08.file1,"
  echo "public08.file2, or public08.file3."
  exit 0
endif
